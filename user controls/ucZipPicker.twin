[Description("")]
[FormDesignerId("1D635DC2-3D7B-4858-BDFC-3311FA053636")]
[ClassId("97288DC2-C853-46E8-BFB6-87938BCF281F")]
[InterfaceId("2557314B-3980-43FC-8013-E90314F83D5F")]
[EventInterfaceId("B54AA7D9-EDD8-4157-8064-786830449DE7")]
[COMControl]
Class ucZipPicker

    ' --- Public Events ---
    Public Event SelectionChanged(ByVal SelectedZip As String)
    Public Event DropDown()

    ' --- Private State ---
    Private m_SelectedZip As String
    Private WithEvents Popup As frmZipPickerPopup
    Private m_IsExpanded As Boolean
    Private mAZipIcon As StdPicture

    '@DefaultMember
    Public Property Get ZipFile() As String
        ZipFile = m_SelectedZip
    End Property

    Public Property Let ZipFile(ByVal Value As String)
        m_SelectedZip = Value
        UserControl.Refresh
        RaiseEvent SelectionChanged(m_SelectedZip)
    End Property
    
    Public Sub Collapse()
        m_IsExpanded = False
        UserControl.Refresh
    End Sub

    Private Sub UserControl_Initialize()
        UserControl.ScaleMode = vbPixels
        UserControl.AutoRedraw = False
        UserControl.Font.Name = "Segoe UI"
        UserControl.Font.Size = 9.75
        m_IsExpanded = False
        
    End Sub

    Private Sub UserControl_Click()
        m_IsExpanded = Not m_IsExpanded
        UserControl.Refresh
        RaiseEvent DropDown()
    End Sub

    Private Sub UserControl_Paint()
        Const PADDING = 5
        Const BUTTON_WIDTH = 30
        Dim xMid As Long, yMid As Long

        ' --- Fill Background ---
        UserControl.FillColor = vbWhite
        UserControl.Line (0, 0)-(ScaleWidth, ScaleHeight), vbWhite, BF

        ' --- Draw Dropdown Button Area ---
        UserControl.FillColor = RGB(0, 122, 204) ' Blue button
        UserControl.Line (ScaleWidth - BUTTON_WIDTH, 0)-(ScaleWidth, ScaleHeight), UserControl.FillColor, BF

        ' --- Draw Arrow (white on blue) ---
        UserControl.ForeColor = vbWhite
        xMid = ScaleWidth - BUTTON_WIDTH / 2
        yMid = ScaleHeight / 2

        If m_IsExpanded Then
            Line (xMid - 4, yMid + 2)-(xMid, yMid - 2)
            Line (xMid + 4, yMid + 2)-(xMid, yMid - 2)
        Else
            Line (xMid - 4, yMid - 2)-(xMid, yMid + 2)
            Line (xMid + 4, yMid - 2)-(xMid, yMid + 2)
        End If

        ' --- Draw Icon + Zip Filename ---
        If Not mAZipIcon Is Nothing Then
            PaintPicture mAZipIcon, PADDING, (ScaleHeight - 16) / 2, 16, 16
        End If

        UserControl.ForeColor = vbBlack  ' Filename text color
        CurrentX = 16 + PADDING
        CurrentY = (ScaleHeight - TextHeight("S")) / 2
        Print m_SelectedZip
        
    End Sub

    Private Sub UserControl_Resize()
        ApplyRoundedCorners UserControl, 4, 4
        UserControl.Refresh
    End Sub

    
End Class