[Description("")]
[FormDesignerId("AE0E71E5-28F7-48E0-985D-A202988830AD")]
[ClassId("014B5795-458A-49AE-839F-E60A19B613DF")]
[InterfaceId("B4BA79FB-6169-4A0E-A5F2-CF0140576517")]
[EventInterfaceId("73D657EC-7E4C-45AB-B989-B29D656C6E7E")]
[COMControl]
Class ucChangeLog

    Option Explicit

    Private Const GWL_WNDPROC As Long = -4
    Private Const WM_MOUSEWHEEL As Long = &H20A
    
    Public canvashWnd As LongPtr

    Private Type Size
        cx As Long
        cy As Long
    End Type

    Public Event ItemClick(ByVal ItemIndex As Long)
    Public Event ItemDoubleClick(ByVal ItemIndex As Long)

    'Private m_Entries() As ChangeLogEntry
    Private m_Entries() As clsChangeLogEntry
    Private m_EntryCount As Long
    Private m_FilterType As String
    Private m_SelectedIndex As Long
    Private m_ScrollOffset As Long

    ' Drawing surface
    Private WithEvents m_Canvas As PictureBox
    Private WithEvents m_HeaderCanvas As PictureBox
    Private WithEvents m_VScrollBar As VScrollBar
    Private m_Columns As Collection
    
    Private Type ChangeLogColumn
        Name As String
        Width As Long
    End Type
        
    ' colors for the type of changle log item
    Private Const COLOR_IMPORTANT = vbBlue
    Private Const COLOR_IMPROVED = RGB(107, 142, 35)      ' other green  
    Private Const COLOR_FIXED = RGB(56, 118, 29) ' green
    Private Const COLOR_UPDATED = RGB(107, 45, 103)       ' Orange badge
    Private Const COLOR_TIP = RGB(22, 83, 126)           ' blueish
    Private Const COLOR_DEFAULT = &H808080       ' Gray badge  
    Private Const COLOR_KNOWN = vbBlack
    Private Const COLOR_ADDED = RGB(75, 0, 130)
    Private Const COLOR_WARNING = RGB(153, 0, 0)
    
    Private Const TEXT_COLOR_WHITE = &HFFFFFF
    Private Const BACKGROUND_COLOR = &HFFFFFF
    Private Const ROW_ALT_COLOR = &HF8F8F8
    Private Const TEXT_COLOR_DARK = &H333333
    Private Const TEXT_COLOR_VERSION = -RGB(128, 49, 116) ' Blue version text
    Private Const SELECTED_COLOR = &HE6F3FF       ' Light blue selection
    Private Const BORDER_COLOR = &HE0E0E0

    Private Const ROW_HEIGHT = 40
    Private Const HEADER_HEIGHT = 500

    Private Sub UserControl_Initialize()
    
        InitializeControls
        
        m_FilterType = "All" ' initially show all types of change log items
        
        ' array of change log entries
        ReDim m_Entries(0)
                
        m_EntryCount = 0
        m_SelectedIndex = -1
        m_ScrollOffset = 0

        'capture mouse scroll
        canvashWnd = m_Canvas.hWnd
        ucDictionary.Add canvashWnd, Me
        
        'originalCanvasProc = SetWindowLongPtr(canvashWnd, GWL_WNDPROC, AddressOf Canvas_WindowProc)
        OriginalCanvasProc = SetWindowLong(canvashWnd, GWL_WNDPROC, AddressOf Canvas_WindowProc)
        
    End Sub

    Private Sub InitializeControls()
        ' Create header canvas
        Set m_HeaderCanvas = Controls.Add("VB.PictureBox", "HeaderCanvas")
        With m_HeaderCanvas
            .Left = 0
            .Top = 0
            .ScaleMode = vbPixels
            .Width = UserControl.ScaleWidth - 200
            .Height = HEADER_HEIGHT
            .BackColor = ROW_ALT_COLOR
            .BorderStyle = 0 ' None
            .AutoRedraw = True
            .Font.Name = "Segoe UI"
            .Font.Size = 11
            .Font.Bold = True
            .Visible = True
            .Cls()
            .Refresh()
        End With
    
        ' Create canvas for the change log items
        Set m_Canvas = Controls.Add("VB.PictureBox", "Canvas")
        With m_Canvas
            .Left = 0
            .Top = HEADER_HEIGHT
            .Width = UserControl.ScaleWidth - 200 ' Leave space for scrollbar
            .Height = UserControl.ScaleHeight - HEADER_HEIGHT
            .BackColor = BACKGROUND_COLOR
            .BorderStyle = 0 ' None
            .AutoRedraw = True
            .ScaleMode = vbPixels
            .Font.Name = "Segoe UI"
            .Font.Size = 10
            .Visible = True
        End With
        
        ' Create vertical scrollbar
        Set m_VScrollBar = Controls.Add("VB.VScrollBar", "VScroll")
        With m_VScrollBar
            .Left = UserControl.ScaleWidth - 200
            .Top = HEADER_HEIGHT
            .Width = 200
            .Height = UserControl.ScaleHeight - HEADER_HEIGHT
            .Min = 0
            .Max = 0
            .Value = 0
            .Visible = False
        End With
    
        DrawHeaders
        
    End Sub

    Public Sub DefineColumns(ParamArray ColumnNames() As Variant)
        Set m_Columns = New Collection
        
        Dim i As Long
        For i = LBound(ColumnNames) To UBound(ColumnNames)
            Dim col As clsChangeLogColumn
            Set col = New clsChangeLogColumn
            
            col.Name = ColumnNames(i)
            col.Width = 100 ' Default, can be adjusted later
            m_Columns.Add col
            
            Set col = Nothing
        Next
        'UserControl.Refresh
        DrawHeaders
        
    End Sub
    
    Private Sub DrawHeaders()
        
        If m_Columns Is Nothing Then Exit Sub
        If m_Columns.Count = 0 Then Exit Sub
        
        Dim hdc As Long
        Dim hFont As Long
        Dim hOldFont As Long
        Dim xPos As Long
        Dim i As Long

        m_HeaderCanvas.Cls
        hdc = m_HeaderCanvas.hDC

        hFont = CreateFont(-12, 0, 0, 0, 700, 0, 0, 0, 0, 0, 0, 0, 0, "Segoe UI")
        If hFont <> 0 Then hOldFont = SelectObject(hdc, hFont)

        SetBkMode hdc, 1
        SetTextColor hdc, TEXT_COLOR_DARK

        xPos = 20
        For i = 1 To m_Columns.Count
            Dim colName As String
            colName = m_Columns(i).Name
            TextOut hdc, xPos, 8, colName, Len(colName)
            xPos = xPos + m_Columns(i).Width
        Next

        If hFont <> 0 Then
            SelectObject hdc, hOldFont
            DeleteObject hFont
        End If

        m_HeaderCanvas.Refresh
    End Sub

    Private Sub m_Canvas_Paint()
        RedrawCanvas
        
    End Sub

    Private Sub RedrawCanvas()
        Dim i As Long
        Dim yPos As Long
        Dim visibleEntries As Long
        Dim startIndex As Long
        Dim endIndex As Long
    
        m_Canvas.Cls
        m_Canvas.BackColor = BACKGROUND_COLOR
    
        ' Calculate visible range
        visibleEntries = Int(m_Canvas.Height / ROW_HEIGHT) + 1
        startIndex = m_ScrollOffset
        endIndex = startIndex + visibleEntries
        If endIndex > GetFilteredEntryCount() Then endIndex = GetFilteredEntryCount()
    
        yPos = 0
        For i = startIndex To endIndex - 1
            DrawRow i, yPos
            yPos = yPos + ROW_HEIGHT
        Next i
    
        m_Canvas.Refresh
        
    End Sub

    Private Sub DrawRow(ByVal entryIndex As Long, ByVal yPos As Long)
        Dim entry As clsChangeLogEntry
        Dim actualIndex As Long
        Dim hdc As Long
        Dim rowRect As RECT
        Dim hBrush As Long
        Dim isSelected As Boolean
        Dim xPos As Long
        Dim i As Long

        actualIndex = GetFilteredEntryIndex(entryIndex)
        If actualIndex = -1 Then Exit Sub

        Set entry = m_Entries(actualIndex)
        isSelected = (actualIndex = m_SelectedIndex)
        hdc = m_Canvas.hDC

        rowRect.Left = 0
        rowRect.Top = yPos
        rowRect.Right = m_Canvas.Width
        rowRect.Bottom = yPos + ROW_HEIGHT

        hBrush = CreateSolidBrush(IIf(isSelected, SELECTED_COLOR, IIf(entryIndex Mod 2 = 0, ROW_ALT_COLOR, BACKGROUND_COLOR)))
        FillRect hdc, rowRect, hBrush
        DeleteObject hBrush

        SetBkMode hdc, 1
        xPos = 20

        For i = 1 To m_Columns.Count
            Dim colName As String
            colName = m_Columns(i).Name

            If UCase(colName) = "TYPE" Then
                ' if there is data in the type text of the entry then draw pill
                If Len(entry.TypeText) > 0 Then DrawTypeBadge(hdc, xPos, yPos + 8, entry)
            Else
                Dim value As String
                On Error Resume Next
                value = entry.Values(colName)
                On Error GoTo 0

                If UCase(colName) = "VERSION" Then
                    SetTextColor hdc, TEXT_COLOR_VERSION
                Else
                    SetTextColor hdc, TEXT_COLOR_DARK
                End If

                TextOut hdc, xPos + 4, yPos + 12, Left(value, 80), Len(Left(value, 80))
            End If

            xPos = xPos + m_Columns(i).Width
        Next
    End Sub

    Private Sub DrawTypeBadge(ByVal hdc As Long, ByVal x As Long, ByVal y As Long, entry As clsChangeLogEntry)
        Dim badgeRect As RECT
        Dim hBrush As Long
        Dim textSize As Size
        Dim badgeWidth As Long
    
        ' Calculate badge size
        GetTextExtentPoint32 hdc, entry.TypeText, Len(entry.TypeText), textSize
        badgeWidth = textSize.cx + 16
    
        ' Set badge rectangle
        badgeRect.Left = x
        badgeRect.Top = y
        badgeRect.Right = x + badgeWidth
        badgeRect.Bottom = y + 24
    
        ' Draw rounded rectangle background
        hBrush = CreateSolidBrush(entry.TypeColor)
        SelectObject hdc, hBrush
        RoundRect hdc, badgeRect.Left, badgeRect.Top, badgeRect.Right, badgeRect.Bottom, 8, 8
        DeleteObject hBrush
    
        ' Draw badge text (white)
        SetBkMode hdc, 1 ' TRANSPARENT
        SetTextColor hdc, TEXT_COLOR_WHITE
        TextOut hdc, x + 8, y + 4, entry.TypeText, Len(entry.TypeText)
    End Sub

    Private Sub m_Canvas_MouseDown(Button As Integer, Shift As Integer, x As Single, y As Single)
        Dim clickedIndex As Long
        Dim actualIndex As Long
    
        If Button = 1 Then ' Left click
            clickedIndex = Int(y / ROW_HEIGHT) + m_ScrollOffset
            actualIndex = GetFilteredEntryIndex(clickedIndex)
        
            If actualIndex <> -1 Then
                m_SelectedIndex = actualIndex
                RedrawCanvas
                RaiseEvent ItemClick(actualIndex)
            End If
        End If
    End Sub

    Private Sub m_Canvas_DblClick()
        If m_SelectedIndex <> -1 Then
            RaiseEvent ItemDoubleClick(m_SelectedIndex)
        End If
    End Sub

    Private Sub m_VScrollBar_Change()
        m_ScrollOffset = m_VScrollBar.Value
        RedrawCanvas
    End Sub

    Private Function GetFilteredEntryCount() As Long
        Dim i As Long
        Dim count As Long
        Dim typeValue As String
        
        On Error Resume Next
        typeValue = m_Entries(i).Values("TYPE")
        On Error GoTo 0
        
        For i = 0 To m_EntryCount - 1
            If m_FilterType = "All" Or UCase(m_FilterType) = UCase(typeValue) Then
                count = count + 1
            End If
        Next i
    
        GetFilteredEntryCount = count
    End Function

    Private Function GetFilteredEntryIndex(ByVal displayIndex As Long) As Long
        Dim i As Long
        Dim count As Long
        Dim typeValue As String
        
        On Error Resume Next
        typeValue = m_Entries(i).Values("TYPE")
        On Error GoTo 0
            
        For i = 0 To m_EntryCount - 1
            If m_FilterType = "All" Or UCase(m_FilterType) = UCase(typeValue) Then
                If count = displayIndex Then
                    GetFilteredEntryIndex = i
                    Exit Function
                End If
                count = count + 1
            End If
        Next i
    
        GetFilteredEntryIndex = -1
    End Function

    Public Sub AddEntry(ParamArray ColumnValues() As Variant)
        
        Dim entry As New clsChangeLogEntry

        ' Initialize the entry using current column definitions and incoming data
        entry.Initialize m_Columns, ColumnValues

        ' Add to entries array
        ReDim Preserve m_Entries(m_EntryCount)
        Set m_Entries(m_EntryCount) = entry
        m_EntryCount = m_EntryCount + 1

        UpdateScrollBar
        RedrawCanvas
    End Sub
    
    Private Sub UpdateScrollBar()

        Dim filteredCount As Long
        Dim visibleRows As Long
    
        filteredCount = GetFilteredEntryCount()
        
        ' check to see if the scrollbar is needed, if so convert the pixel row height to twips to calculate that
        If Not m_Canvas Is Nothing Then visibleRows = Int(m_Canvas.Height / (ROW_HEIGHT * Screen.TwipsPerPixelX))
    
        If filteredCount > visibleRows Then
            m_VScrollBar.Max = filteredCount - visibleRows
            m_VScrollBar.Visible = True
        Else
            m_VScrollBar.Visible = False
            m_ScrollOffset = 0
        End If
    End Sub

    Public Sub ClearEntries()
        ReDim m_Entries(0)
        m_EntryCount = 0
        m_SelectedIndex = -1
        m_ScrollOffset = 0
        UpdateScrollBar
        RedrawCanvas
    End Sub

    Public Sub SetFilter(ByVal FilterType As String)
        m_FilterType = FilterType
        m_SelectedIndex = -1
        m_ScrollOffset = 0
        UpdateScrollBar
        RedrawCanvas
    End Sub

    Public Sub LoadSampleFullChangeLogData()
        
        ClearEntries

        DefineColumns "VERSION", "TYPE", "DESCRIPTION"
        AddEntry "820", "IMPORTANT", "This is an interim/experimental release. It includes significant changes, so some instability is to be expected."
        AddEntry "820", "IMPROVED", "Markdown preview mode in the editor now allows for selection of text"
        AddEntry "820", "FIXED", "Auto-correct editor bug sometimes causing invalid ""Private Sub"" to be inserted when creating an event from the form designer"
        AddEntry "820", "UPDATED", "Sample 15 (GlobalSearchAddIn) to match the latest version of GlobalSearchAddIn that is included with the IDE"
        AddEntry "820", "FIXED", "After the recent package changes, the ""Available Packages"" list might sometimes contain enabled packages"

        AddEntry "820", "IMPORTANT", "This is an interim/experimental release. It includes significant changes, so some instability is to be expected."
        AddEntry "820", "IMPROVED", "Markdown preview mode in the editor now allows for selection of text"
        AddEntry "820", "FIXED", "Auto-correct editor bug sometimes causing invalid ""Private Sub"" to be inserted when creating an event from the form designer"
        AddEntry "820", "UPDATED", "Sample 15 (GlobalSearchAddIn) to match the latest version of GlobalSearchAddIn that is included with the IDE"
        AddEntry "820", "FIXED", "After the recent package changes, the ""Available Packages"" list might sometimes contain enabled packages"
                
    End Sub

    Public Sub LoadSampleRevertChangeLogData()
        
        ClearEntries

        DefineColumns "TYPE", "DESCRIPTION"
        
        AddEntry "IMPORTANT", "This is an interim/experimental release. It includes significant changes, so some instability is to be expected."
        AddEntry "IMPROVED", "Markdown preview mode in the editor now allows for selection of text"
        AddEntry "FIXED", "Auto-correct editor bug sometimes causing invalid ""Private Sub"" to be inserted when creating an event from the form designer"
        AddEntry "UPDATED", "Sample 15 (GlobalSearchAddIn) to match the latest version of GlobalSearchAddIn that is included with the IDE"
        AddEntry "FIXED", "After the recent package changes, the ""Available Packages"" list might sometimes contain enabled packages"

        AddEntry "IMPORTANT", "This is an interim/experimental release. It includes significant changes, so some instability is to be expected."
        AddEntry "IMPROVED", "Markdown preview mode in the editor now allows for selection of text"
        AddEntry "FIXED", "Auto-correct editor bug sometimes causing invalid ""Private Sub"" to be inserted when creating an event from the form designer"
        AddEntry "UPDATED", "Sample 15 (GlobalSearchAddIn) to match the latest version of GlobalSearchAddIn that is included with the IDE"
        AddEntry "FIXED", "After the recent package changes, the ""Available Packages"" list might sometimes contain enabled packages"
                
    End Sub
    
    Public Sub LoadSampleHistoryActivityLogData()
        
        ClearEntries
        
        AddEntry "07/15/25 10:01:04 AM", "", "", "Checking for Settings file - found and loaded"
        AddEntry "07/15/25 10:01:24 AM", "", "", "Auto check for new version"
        AddEntry "07/15/25 10:01:44 AM", "", "", "Newest version available is 820"
        AddEntry "07/15/25 10:02:04 AM", "", "", "Downloading twinBASIC 807 - done"
        AddEntry "07/15/25 10:02:24 AM", "", "", "Executing Post Download action of installing twinBASIC"
        AddEntry "07/15/25 10:02:44 AM", "", "", "twinBASIC from f:\downloads\twinBASIC_IDE_BETA_820.zip has been extracted and is ready to use"
        AddEntry "07/15/25 10:03:04 AM", "", "", "Post Download action complete"
        AddEntry "07/15/25 10:03:24 AM", "", "", "Process complete"
        
        AddEntry "07/15/25 10:03:44 AM", "820", "IMPORTANT", "This is an interim/experimental release. It includes significant changes, so some instability is to be expected."
        AddEntry "07/15/25 10:03:44 AM", "820", "IMPROVED", "Markdown preview mode in the editor now allows for selection of text"
        AddEntry "07/15/25 10:03:44 AM", "820", "FIXED", "Auto-correct editor bug sometimes causing invalid ""Private Sub"" to be inserted when creating an event from the form designer"
        AddEntry "07/15/25 10:03:45 AM", "820", "UPDATED", "Sample 15 (GlobalSearchAddIn) to match the latest version of GlobalSearchAddIn that is included with the IDE"
        AddEntry "07/15/25 10:03:45 AM", "820", "FIXED", "After the recent package changes, the ""Available Packages"" list might sometimes contain enabled packages"
                        
    End Sub
        
    Private Sub UserControl_Resize()
        If Not m_Canvas Is Nothing Then
            m_HeaderCanvas.Width = UserControl.ScaleWidth - 200
        
            m_Canvas.Width = UserControl.ScaleWidth - 200
            m_Canvas.Height = UserControl.ScaleHeight - HEADER_HEIGHT
        
            m_VScrollBar.Left = UserControl.ScaleWidth - 200
            m_VScrollBar.Height = UserControl.ScaleHeight - HEADER_HEIGHT
        
            UpdateScrollBar
            DrawHeaders
            RedrawCanvas
        End If
    End Sub

    ' Properties
    Public Property Get Filter() As String
        Filter = m_FilterType
    End Property

    Public Property Let Filter(ByVal Value As String)
        SetFilter Value
    End Property

    Public Property Get EntryCount() As Long
        EntryCount = m_EntryCount
    End Property

    Public Property Get SelectedIndex() As Long
        SelectedIndex = m_SelectedIndex
    End Property
    
    Public Sub HandleMouseScroll(ByVal zDelta As Long)

        If Not m_VScrollBar.Visible Then Exit Sub

        Dim direction As Long
        direction = Sgn(zDelta)

        Dim newVal As Long
        newVal = m_VScrollBar.Value - direction

        If newVal < m_VScrollBar.Min Then newVal = m_VScrollBar.Min
        If newVal > m_VScrollBar.Max Then newVal = m_VScrollBar.Max

        If newVal <> m_VScrollBar.Value Then
            m_VScrollBar.Value = newVal
            m_ScrollOffset = newVal
            RedrawCanvas
        End If
    End Sub

    Private Sub UserControl_Terminate()
        
        ' have to release things when destorying the control
        If canvashWnd <> 0 And OriginalCanvasProc <> 0 Then
            Call SetWindowLong(canvashWnd, GWL_WNDPROC, OriginalCanvasProc)
            canvashWnd = 0
            OriginalCanvasProc = 0
        End If
        
    End Sub
    
End Class